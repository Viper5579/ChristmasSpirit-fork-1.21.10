plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'eclipse'
    id 'maven-publish'
}

version = "${mod_version}"
group = 'com.tm.cspirit'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {

    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'cspirit'

            mods {
                cspirit {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'cspirit'

            mods {
                cspirit {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'cspirit', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                cspirit {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // JEI API for 1.21.1
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"
}

// Force correct LWJGL natives for Apple Silicon BEFORE other configurations
if (System.getProperty("os.arch").contains("aarch64") && System.getProperty("os.name").contains("Mac")) {
    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                substitute platform("org.lwjgl:lwjgl-bom:3.3.3") using platform("org.lwjgl:lwjgl-bom:3.3.4")
            }
            force "org.lwjgl:lwjgl:3.3.4"
            force "org.lwjgl:lwjgl-glfw:3.3.4"
            force "org.lwjgl:lwjgl-opengl:3.3.4"
            force "org.lwjgl:lwjgl-stb:3.3.4"
            force "org.lwjgl:lwjgl-tinyfd:3.3.4"
            force "org.lwjgl:lwjgl-jemalloc:3.3.4"
            force "org.lwjgl:lwjgl-freetype:3.3.4"
        }
    }
}

// Apple Silicon (M-series Mac) LWJGL natives fix
// Replace non-existent 'natives-macos-patch' with 'natives-macos-arm64'
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.lwjgl' && details.requested.name.endsWith('-natives-macos-patch')) {
            // Extract base module name (e.g., 'lwjgl-freetype' from 'lwjgl-freetype-natives-macos-patch')
            def baseName = details.requested.name.replaceAll('-natives-macos-patch$', '')
            // Replace with the correct arm64 variant
            details.useTarget(
                group: 'org.lwjgl',
                name: "${baseName}-natives-macos-arm64",
                version: '3.3.4'
            )
            println "Replaced LWJGL dependency: ${details.requested.name} -> ${baseName}-natives-macos-arm64:3.3.4"
        }
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Christmas Spirit",
                "Specification-Vendor"    : "TeamMonumental",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "TeamMonumental",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}